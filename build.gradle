group 'ca.joelathiessen.kaly2'
version '0.1'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.1.4-3"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

mainClassName = 'ca.joelathiessen.kaly2.server.KalyServer'

repositories {
    mavenCentral()
    jcenter()
}

jar {
    manifest {
        attributes 'Main-Class': 'ca.joelathiessen.kaly2.server.KalyServer'
    }

    doFirst {
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}

sourceSets {
    test {
        resources {
            srcDir 'images'
        }
    }
}

configurations {
    ktlint
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

dependencies {
    ktlint 'com.github.shyiko:ktlint:0.9.0'

    compile "org.jetbrains.kotlin:kotlin-stdlib:1.1.4-3"
    compile 'gov.nist.math:jama:1.0.3'
    compile files("lib/leJOS_EV3_0.9.1-beta/lib/ev3/ev3classes.jar")
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.github.salomonbrys.kotson:kotson:2.3.0'

    // Jetty versions 8+ are incompatible with Android, so use the last 7x release
    compile 'org.eclipse.jetty:jetty-server:7.6.19.v20160209'
    compile 'org.eclipse.jetty:jetty-servlet:7.6.19.v20160209'
    compile 'org.eclipse.jetty:jetty-http:7.6.19.v20160209'
    compile 'org.eclipse.jetty:jetty-websocket:7.6.19.v20160209'

    testCompile 'junit:junit:4.12'
    testCompile 'org.powermock:powermock-api-mockito:1.6.1'
    testCompile 'org.powermock:powermock-module-junit4-rule-agent:1.6.1'
    testCompile 'org.powermock:powermock-module-junit4-rule:1.6.1'
    testCompile 'org.powermock:powermock-module-junit4:1.6.1'
    testCompile 'com.nhaarman:mockito-kotlin:0.4.0'
}
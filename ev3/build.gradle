buildscript {
    ext.kotlin_version = "1.1.51"
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.github.bdeneuter.mindstorms.ev3" version "1.2.0"
}

group 'ca.joelathiessen.kaly2.ev3'
version '0.1'


apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'application'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

mainClassName = 'ca.joelathiessen.kaly2.ev3.MainLoop'

repositories {
    mavenCentral()
    jcenter()
}


jar {
    manifest {
        attributes 'Main-Class': 'ca.joelathiessen.kaly2.ev3.MainLoop'
    }

    doFirst {
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}

configurations {
    ktlint
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}

dependencies {
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.github.salomonbrys.kotson:kotson:2.3.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.8.9'
    testCompile 'com.nhaarman:mockito-kotlin:1.5.0'
    ktlint "com.github.shyiko:ktlint:0.20.0"
}

mindstorms {
    main = 'ca.joelathiessen.kaly2.ev3.MainLoop'
}
